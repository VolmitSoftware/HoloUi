/*
 * HoloUI is a holographic user interface for Minecraft Bukkit Servers
 * Copyright (c) 2025 Arcane Arts (Volmit Software)
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <https://www.gnu.org/licenses/>.
 */

import org.gradle.api.tasks.Copy
import org.gradle.api.tasks.compile.JavaCompile
import org.gradle.jvm.toolchain.JavaLanguageVersion

plugins {
    id 'java-library'
    alias(libs.plugins.lombok)
    alias(libs.plugins.shadow)
    alias(libs.plugins.slimjar)
}

group = 'art.arcane'
version = '1.0.0-1.21.11'
def apiVersion = '1.21'
def main = 'art.arcane.holoui.HoloUI'
def lib = 'art.arcane.holoui.libs'
String volmLibCoordinate = providers.gradleProperty('volmLibCoordinate')
    .orElse('com.github.VolmitSoftware:VolmLib:master-SNAPSHOT')
    .get()

def shadowJarTask = tasks.named('shadowJar', com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar)

// ADD YOURSELF AS A NEW LINE IF YOU WANT YOUR OWN BUILD TASK GENERATED
// ======================== WINDOWS =============================
registerCustomOutputTask(
    'Cyberpwn',
    'C://Users/cyberpwn/Documents/development/server/plugins'
)
registerCustomOutputTask(
    'Psycho',
    'C://Dan/MinecraftDevelopment/Server/plugins'
)
registerCustomOutputTask(
    'ArcaneArts',
    'C://Users/arcane/Documents/development/server/plugins'
)
registerCustomOutputTask('Vatuu', 'D://Minecraft/Servers/1.20/plugins')
registerCustomOutputTask('Nowhere', 'E://Desktop/server/plugins')
registerCustomOutputTask(
    'CrazyDev22',
    'C://Users/Julian/Desktop/server/plugins'
)
registerCustomOutputTask(
    'Pixel',
    'D://Iris Dimension Engine//1.20.4 - Development//plugins'
)
// ========================== UNIX ==============================
registerCustomOutputTaskUnix(
    'CyberpwnLT',
    '/Users/danielmills/development/server/plugins'
)
registerCustomOutputTaskUnix(
    'PsychoLT',
    '/Users/brianfopiano/Developer/RemoteGit/[Minecraft Server]/consumers/plugin-consumers/dropins/plugins'
)
registerCustomOutputTaskUnix(
    'the456gamer',
    '/home/the456gamer/projects/minecraft/adapt-testserver/plugins/update/',
    false
)
// ==============================================================

tasks.named('jar').configure {
    enabled = false
}

tasks.named('build').configure {
    dependsOn(shadowJarTask)
}

tasks.named('compileJava', JavaCompile).configure {
    options.compilerArgs.add('-parameters')
    options.encoding = 'UTF-8'
    options.release.set(21)
}

shadowJarTask.configure {
    archiveClassifier.set('')
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    minimize()

    relocate('io.github.slimjar', "${lib}.slimjar")
    relocate('com.github.retrooper.packetevents', "${lib}.packetevents.api")
    relocate('io.github.retrooper.packetevents', "${lib}.packetevents.impl")
    relocate('org.bstats', "${lib}.bstats")
}

tasks.named('processResources').configure {
    def pluginProperties = [
        name      : rootProject.name,
        version   : project.version,
        main      : main,
        apiVersion: apiVersion,
    ]

    inputs.properties(pluginProperties)
    filesMatching('**/plugin.yml') {
        expand(pluginProperties)
    }
}

slimJar {
    relocate('com.github.retrooper.packetevents', "${lib}.packetevents.api")
    relocate('io.github.retrooper.packetevents', "${lib}.packetevents.impl")
    relocate('org.bstats', "${lib}.bstats")
    relocate('org.apache.commons', "${lib}.commons")
    relocate('com.github.zafarkhaja.semver', "${lib}.semver")
    relocate('io.undertow', "${lib}.undertow")
    relocate('org.jboss', "${lib}.jboss")
    relocate('org.xnio', "${lib}.xnio")
    relocate('org.wildfly', "${lib}.wildfly")
    relocate('net.kyori', "${lib}.kyori")
}

repositories {
    mavenCentral()
    maven { url = uri('https://libraries.minecraft.net/') }
    maven { url = uri('https://hub.spigotmc.org/nexus/content/repositories/snapshots/') }
    maven { url = uri('https://repo.papermc.io/repository/maven-public/') }
    maven { url = uri('https://repo.extendedclip.com/releases/') }
    maven { url = uri('https://repo.codemc.io/repository/maven-releases/') }
    maven { url = uri('https://repo.aikar.co/content/groups/aikar/') }
    maven { url = uri('https://jitpack.io') }
}

dependencies {
    implementation('de.crazydev22.slimjar.helper:spigot:2.1.8')
    implementation(volmLibCoordinate) {
        changing = true
        transitive = false
    }

    compileOnly(libs.lombok)
    annotationProcessor(libs.lombok)
    compileOnly(libs.spigot)
    compileOnly(libs.placeholderApi)

    // Shaded
    slim(libs.packetevents) {
        exclude(group: 'net.kyori')
    }
    slim('org.bstats:bstats-bukkit:3.1.0')

    // Dynamically loaded
    slim(libs.adventure.minimessage)
    slim(libs.adventure.nbt)
    slim(libs.undertow)
    slim(libs.semver)
    slim(libs.commons.io)
    slim(libs.commons.imaging)
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

configurations.configureEach {
    resolutionStrategy.cacheChangingModulesFor(0, 'seconds')
    resolutionStrategy.cacheDynamicVersionsFor(0, 'seconds')
}

if (!JavaVersion.current().isCompatibleWith(JavaVersion.VERSION_21)) {
    System.err.println()
    System.err.println('=========================================================================================================')
    System.err.println('You must run gradle on Java 21 or newer. You are using ' + JavaVersion.current())
    System.err.println()
    System.err.println('=== For IDEs ===')
    System.err.println('1. Configure the project for Java 21')
    System.err.println('2. Configure the bundled gradle to use Java 21 in settings')
    System.err.println()
    System.err.println('=== For Command Line (gradlew) ===')
    System.err.println('1. Install JDK 21 from https://www.oracle.com/java/technologies/downloads/#java21')
    System.err.println('2. Set JAVA_HOME environment variable to the new jdk installation folder')
    System.err.println('3. Open a new command prompt window to get the new environment variables if need be.')
    System.err.println('=========================================================================================================')
    System.err.println()
    System.exit(69)
}

// IDE Server stuff
void registerCustomOutputTask(String name, String path, boolean doRename = true) {
    if (!System.getProperty('os.name').toLowerCase().contains('windows')) {
        return
    }

    createOutputTask(name, path, doRename)
}

void registerCustomOutputTaskUnix(String name, String path, boolean doRename = true) {
    if (System.getProperty('os.name').toLowerCase().contains('windows')) {
        return
    }

    createOutputTask(name, path, doRename)
}

void createOutputTask(String name, String path, boolean doRename = true) {
    def shadowJarTask = tasks.named('shadowJar', com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar)
    tasks.register("build${name}", Copy) {
        group = 'development'
        outputs.upToDateWhen { false }
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
        dependsOn(shadowJarTask)
        from(shadowJarTask.flatMap { it.archiveFile })
        into(file(path))
        if (doRename) {
            rename { String ignored -> 'HoloUi.jar' }
        }
    }
}
